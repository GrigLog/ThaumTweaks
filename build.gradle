buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }       
		maven {
            name = "github"
            url = "https://github.com/juanmuscaria/maven/raw/master"
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle2:[2.3-1.0.0-SNAPSHOT,)'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    //maven { url = "https://dist.creeper.host/Sponge/maven" }
	flatDir {
		dirs 'libs'
	}
}

version = mod_version
group = mod_group
archivesBaseName = mod_archives_name
idea.module.inheritOutputDirs = true  //without this models dont load for some reason...

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
	version = mc_version + "-" + forge_version
    mappings = "snapshot_20171003"
	runDir = "run"
    
	replace '@VERSION@', project.version
	// makeObfSourceJar = false
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version,
		'mod_id': mod_id,
		'mod_name': mod_name,
		'mod_author': mod_author,
		'mod_description': mod_description,
		'mc_version': mc_version,
		'mod_version': mod_version,
		'mod_icon': mod_icon
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

configurations {
    extraLibs
    implementation.extendsFrom(extraLibs)
}
dependencies {
	//compile name: 'Baubles-1.12-1.5.2-dev'
    extraLibs('org.spongepowered:mixin:0.8-SNAPSHOT') { transitive = false }
    compile files('libs/Thaumcraft-1.12.2-6.1.BETA26-deobf.jar')
}

minecraft.coreMod = "griglog.thaumtweaks.core.TTPlugin"
jar {
    manifest.attributes(
            "FMLCorePluginContainsFMLMod": true,
            //"ForceLoadAsMod": true,  //disable in dev environment
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            "TweakOrder": 0,
            'MixinConfigs': 'mixins.thaumtweaks.json',
            'FMLCorePlugin': 'griglog.thaumtweaks.core.TTPlugin'
    )
    from(configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }){
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.thaumtweaks.refmap.json"
    }
}

task devJar(type: Jar) {
	classifier = 'dev'
	from sourceSets.main.output
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives devJar
	archives sourcesJar
}